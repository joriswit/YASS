apply plugin: 'com.android.application'

android {
    compileSdk 34
    namespace 'yass'

    defaultConfig {
        applicationId "net.sourceforge.sokobanyasc.joriswit.yass"
        minSdk 14
        targetSdk 34
        versionCode 12
        versionName "2.145"
        resConfigs "en"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    //compile fileTree(dir: 'libs', include: ['*.jar'])
    //testCompile 'junit:junit:4.12'
    implementation 'androidx.core:core:1.12.0' // final version supporting API 14
    implementation 'androidx.annotation:annotation:1.9.0'
}

def compilePascal(String taskName, String compiler, String outputDir) {
    tasks.create(name: taskName, type: Exec) {
        description = "Compile Pascal source files using ${compiler}"
        group = 'build'

        def pascalJniSource = file('src/main/jni-pascal/yassjni.lpr')
        def pascalSource = file('src/main/jni-pascal/YASS.pas')
        def outputFile = file("${outputDir}/libyassjni.so")

        inputs.files(pascalJniSource, pascalSource)
        outputs.file outputFile

        workingDir projectDir

        commandLine compiler, '-Tandroid', '-O2', '-FE' + outputDir, pascalJniSource
    }
}

compilePascal('compilePascalArm', 'ppcrossarm', 'src/main/jniLibs/armeabi-v7a')
compilePascal('compilePascalX86', 'ppcross386', 'src/main/jniLibs/x86')
compilePascal('compilePascalArm64', 'ppcrossa64', 'src/main/jniLibs/arm64-v8a')
compilePascal('compilePascalX86_64', 'ppcrossx64', 'src/main/jniLibs/x86_64')

preBuild.dependsOn compilePascalArm, compilePascalX86, compilePascalArm64, compilePascalX86_64

